ext {
    nodejs_version = '10.8.0'
    kotlinx_html_version = "0.6.4"
    react_version = "16.4.2-pre.38-kotlin-$kotlin_version"
    react_dom_version = "16.4.2-pre.38-kotlin-$kotlin_version"
    react_router_dom_version = "4.3.1-pre.38-kotlin-$kotlin_version"
}

buildscript {
    ext {
        kotlin_frontend_version = '0.0.36'
    }
    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:$kotlin_frontend_version"
    }
}

apply plugin: 'org.jetbrains.kotlin.frontend'
apply plugin: 'kotlin2js'
apply plugin: 'kotlin-platform-js'

compileKotlin2Js {
    kotlinOptions {
        metaInfo = true
        sourceMap = true
        moduleKind = 'commonjs'
        kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
        main = "call"
    }
}

def webpack_config = build_env == "production" ? 'webpack.prod.js' : 'webpack.dev.js'

kotlinFrontend {
    downloadNodeJsVersion = nodejs_version
    npm {
        dependency "react"
        dependency "react-dom"
        dependency "react-router-dom"
        devDependency("webpack", "4.16.5")
        devDependency("html-webpack-plugin", "^3.2.0")
        devDependency("clean-webpack-plugin", "^0.1.19")
        devDependency("css-loader", "^0.28.11")
        devDependency("file-loader", "^1.1.11")
        devDependency("style-loader", "^0.21.0")
        devDependency("webpack-merge", "4.1.4")
        devDependency("uglifyjs-webpack-plugin", "1.2.7")
    }
    webpackBundle {
        bundleName = "main"
        webpackConfigFile = project.projectDir.path + '/' + webpack_config
    }
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlinx/kotlinx" }
    maven { url "http://dl.bintray.com/kotlin/kotlin-js-wrappers" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
    compile "org.jetbrains:kotlin-react:$react_version"
    compile "org.jetbrains:kotlin-react-dom:$react_dom_version"
    compile "org.jetbrains:kotlin-react-router-dom:$react_router_dom_version"
    expectedBy project(':common')
    compile project(':common-js')
}